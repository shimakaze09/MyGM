# Set the minimum required version of CMake
CMAKE_MINIMUM_REQUIRED(VERSION 3.14 FATAL_ERROR)

# Set the project
PROJECT(MyGM VERSION 0.3.6 LANGUAGES CXX)
MESSAGE(STATUS "Project: ${PROJECT_NAME} ${PROJECT_VERSION}")

# Add 'd' postfix for debug builds
SET(CMAKE_DEBUG_POSTFIX "d")

# Set C++ version
SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)

IF (MSVC)
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /permissive-")
ENDIF ()

# Set the output directories
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/bin")
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/bin")
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/bin")
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib")
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/lib")
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/lib")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG "${PROJECT_SOURCE_DIR}/lib")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE "${PROJECT_SOURCE_DIR}/lib")

# Turn on the Visual Studio folders
SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

# Add CMake modules
INCLUDE(FETCHCONTENT)

MESSAGE(STATUS "Looking for: MyCMake v0.3.3")
FIND_PACKAGE(MyCMake 0.3.3 QUIET)
IF (${MyCMake_FOUND})
    MESSAGE(STATUS "MyCMake v${MyCMake_VERSION} found")
ELSE ()
    SET(ADDRESS "https://github.com/shimakaze09/MyCMake.git")
    MESSAGE(STATUS "MyCMake v0.3.3 not found\n"
            "fetch from ${ADDRESS} with tag v0.3.3")
    FETCHCONTENT_DECLARE(
            MyCMake
            GIT_REPOSITORY ${ADDRESS}
            GIT_TAG "v0.3.3"
    )
    message(STATUS "Building MyCMake v0.3.3")
    FETCHCONTENT_MAKEAVAILABLE(MyCMake)
    MESSAGE(STATUS "MyCMake v0.3.3 built")
ENDIF ()

# Set default build path prefix
INIT_INSTALL_PREFIX()

ADD_DEP(MyTemplate 0.2.0)

ADD_SUB_DIRS_REC(src)

ADD_NATVIS()

EXPORT_TARGETS(DIRECTORIES "include")